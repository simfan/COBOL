       IDENTIFICATION DIVISION.

       PROGRAM-ID. CBL01.
       AUTHOR.  CHRIS GLOCK.
       DATE-WRITTEN. 01/13/06.
       DATE-COMPILED.
      ******************************************************************
      * PURPOSE:  THE PURPOSE OF THIS PROGRAM IS TO CREATE A BILLING
      * REPORT FOR THE LIGHT-EM-UP UTILITY COMPANY.  THE REPORT WILL
      * CONTIAIN INFORMATION ON EACH CUSTOMER, INCLUDING NAME, ADDRESS
      * KILOWAT HOURS, GAS USED, THE ELECTRIC BILL, THE GAS BILL, AND
      * AND THE TOTAL BILL.
      ******************************************************************

       ENVIRONMENT DIVISION.

       INPUT-OUTPUT SECTION.

       FILE-CONTROL.

           SELECT BILLING-INFO
               ASSIGN TO CBL01IN.

           SELECT GAS-BILL-INFO
               ASSIGN TO CBL01GB.

           SELECT ELEC-BILL-INFO
               ASSIGN TO CBL01EB.

           SELECT BILLING-RPT
               ASSIGN TO CBL1RPT.

       DATA DIVISION.


       FILE SECTION.

       FD   BILLING-INFO
            RECORD CONTAINS 65 CHARACTERS.

       01   BILLING-DATA.
            05 BD-ACCOUNT               PIC 9(5).
            05 BD-CUST-NAME             PIC X(20).
            05 BD-CUST-ADDRESS          PIC X(20).
            05 BD-KILOWATT              PIC 9(5).
            05 BD-GAS                   PIC 9(5).
            05 BD-ELEC                  PIC 9(3)V99.
            05 BD-GAS-BILL              PIC 9(3)V99.

       FD   GAS-BILL-INFO
            RECORD CONTAINS 55 CHARACTERS.

       01   GAS-BILL-DATA.
            05 GB-ACCOUNT               PIC 9(5).
            05 GB-CUST-NAME             PIC X(20).
            05 GB-CUST-ADDRESS          PIC X(20).
            05 GB-GAS-USED              PIC 9(5).
            05 GB-GAS-BILL              PIC 9(3)V99.

       FD   ELEC-BILL-INFO
            RECORD CONTAINS 55 CHARACTERS.

       01   ELEC-BILL-DATA.
            05 EB-ACCOUNT               PIC 9(5).
            05 EB-CUST-NAME             PIC X(20).
            05 EB-CUST-ADDRESS          PIC X(20).
            05 EB-KILO-HOURS            PIC 9(5).
            05 EB-ELEC-BILL             PIC 9(3)V99.

       FD   BILLING-RPT
            RECORD CONTAINS 132 CHARACTERS.

       01 PRINT-REC                     PIC X(132).


       WORKING-STORAGE SECTION.
      ******************************************************************
      * THIS IS WHERE YOU DEFINE VARIABLES NEEDED THAT ARE NOT
      * PART OF THE ANY FILE DEFINITION. (REMOVE)
      ******************************************************************

       01  WORK-FIELDS.
           05 MORE-RECORDS  PIC X(3) VALUE 'YES'.

       01  PRINT-CONTROL.
           05 WS-LINE-CNT   PIC 9(2) VALUE 60.
           05 WS-PAGE-CNT   PIC 9(3).
           05 WS-LINES      PIC 9 VALUE 2.

       01  TOTAL-FIELDS.
           05 TOT-BILL      PIC 9(4)V99.
           05 TOT-CUST      PIC 9(3).

       01  CURRENT-DATE-AND-TIME.
           05 CD-YEAR       PIC 9(4).
           05 CD-MONTH      PIC 9(2).
           05 CD-DAY        PIC 9(2).
           05 CD-HOUR       PIC 9(2).
           05 CD-MINUTE     PIC 9(2).
           05               PIC X(9).

       01  TSO-ID           PIC X(7) VALUE 'XSCS119'.

       01  HEADING-LINE-1.
           05               PIC X(6) VALUE 'DATE: '.
           05 HL1-MONTH     PIC 9(2).
           05               PIC X VALUE '/'.
           05 HL1-DAY       PIC 9(2).
           05               PIC X VALUE '/'.
           05 HL1-YEAR      PIC 9(4).
           05               PIC X(36) VALUE SPACES.
           05          PIC X(29) VALUE 'LIGHT-EM-UP  UTILITY  COMPANY'.
           05               PIC X(28) VALUE SPACES.
           05               PIC X(5) VALUE 'PAGE '.
           05 HL1-PAGE      PIC ZZ9.

       01  HEADING-LINE-2.
           05               PIC X(6) VALUE 'TIME: '.
           05 HL2-HOUR      PIC 9(2).
           05               PIC X VALUE ':'.
           05 HL2-MINUTE    PIC 9(2).

       01  HEADING-LINE-3.
           05               PIC X(59) VALUE SPACES.
           05               PIC X(15) VALUE 'BILLING  REPORT'.

       01  HEADING-UNDER-LINE-1.
           05               PIC X(59) VALUE SPACES.
           05               PIC X(15) VALUE ALL '_'.

       01  HEADING-LINE-4.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(7) VALUE 'ACCOUNT'.
           05               PIC X(55) VALUE SPACES.
           05               PIC X(8) VALUE 'KILOWATT'.
           05               PIC X(5) VALUE SPACES.
           05               PIC X(3) VALUE 'GAS'.
           05               PIC X(6) VALUE SPACES.
           05               PIC X(8) VALUE 'ELECTRIC'.
           05               PIC X(6) VALUE SPACES.
           05               PIC X(3) VALUE 'GAS'.
           05               PIC X(10) VALUE SPACES.
           05               PIC X(5) VALUE 'TOTAL'.

       01  HEADING-LINE-5.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(6) VALUE 'NUMBER'.
           05               PIC X(8) VALUE SPACES.
           05               PIC X(9) VALUE 'CUSTOMER '.
           05               PIC X(4) VALUE 'NAME'.
           05               PIC X(17) VALUE SPACES.
           05               PIC X(7) VALUE 'ADDRESS'.
           05               PIC X(12) VALUE SPACES.
           05               PIC X(5) VALUE 'HOURS'.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(4) VALUE 'USED'.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(4) VALUE 'BILL'.
           05               PIC X(9) VALUE SPACES.
           05               PIC X(4) VALUE 'BILL'.

       01  HEADING-UNDER-LINE-2.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(6) VALUE ALL '_'.
           05               PIC X(8) VALUE SPACES.
           05               PIC X(9) VALUE ALL '_'.
           05               PIC X(4) VALUE ALL '_'.
           05               PIC X(17) VALUE SPACES.
           05               PIC X(7) VALUE ALL '_'.
           05               PIC X(12) VALUE SPACES.
           05               PIC X(5) VALUE ALL '_'.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(4) VALUE ALL '_'.
           05               PIC X(7) VALUE SPACES.
           05               PIC X(4) VALUE ALL '_'.
           05               PIC X(9) VALUE SPACES.
           05               PIC X(4) VALUE ALL '_'.

       01  DETAIL-LINE.
           05               PIC X(7) VALUE SPACES.
           05 DL-ACCOUNT    PIC 9(5).
           05               PIC X(6) VALUE SPACES.
           05 DL-CUST-NAME  PIC X(20).
           05               PIC X(6) VALUE SPACES.
           05 DL-ADDRESS    PIC X(20).
           05               PIC X(6) VALUE SPACES.
           05 DL-KW-HOURS   PIC 9(5).
           05               PIC X(6) VALUE SPACES.
           05 DL-GAS-USED   PIC 9(5).
           05               PIC X(6) VALUE SPACES.
           05 DL-ELEC-BILL  PIC ZZ9.99.
           05               PIC X(6) VALUE SPACES.
           05 DL-GAS-BILL   PIC ZZ9.99.
           05               PIC X(6) VALUE SPACES.
           05 DL-TOTAL-BILL PIC $Z,ZZ9.99.

       01  TOTAL-LINE.
           05               PIC X(21) VALUE SPACES.
           05               PIC X(13) VALUE '*****  TOTAL '.
           05               PIC X(11) VALUE 'CUSTOMERS  '.
           05 TL-CUSTOMERS  PIC ZZ9.

       PROCEDURE DIVISION.
      *******************************************
      *  MAIN PROCESSING MODULE.
      *   - HOUSEKEEPING PERFORMED
      *   - PRETEST LOOP IS PERFORMED UNTIL ALL RECORDS ARE READ
      *     - FOR EACH RECORD READ, 110 PROCESS IS PERFORMED
      *   - END OF JOB PERFORMED
      *******************************************

       000-MAIN-MODULE.

           PERFORM 100-HOUSEKEEPING.

           PERFORM UNTIL MORE-RECORDS = 'NO '
             READ BILLING-INFO
               AT END
                 MOVE 'NO ' TO MORE-RECORDS
               NOT AT END
                 PERFORM 110-PROCESS
             END-READ
           END-PERFORM.

           PERFORM 120-END-OF-JOB.

           STOP RUN.

      *******************************************
      *  HOUSEKEEPING MODULE.
      *  - FILES OPENED FOR PROCESSING
      *******************************************

       100-HOUSEKEEPING.

           OPEN INPUT BILLING-INFO
                OUTPUT GAS-BILL-INFO
                       ELEC-BILL-INFO
                       BILLING-RPT.

           MOVE FUNCTION CURRENT-DATE TO CURRENT-DATE-AND-TIME.
           MOVE CD-MONTH TO HL1-MONTH.
           MOVE CD-DAY TO HL1-DAY.
           MOVE CD-YEAR TO HL1-YEAR.
           MOVE CD-HOUR TO HL2-HOUR.
           MOVE CD-MINUTE TO HL2-MINUTE.
      *******************************************
      *  DETAIL PROCESS MODULE.
      *  - DATA IS MOVED FROM INPUT BUFFER TO THE OUTPUT BUFFERS
      *  - GAS BILL IS CALCULATED
      *  - ELECTRIC BILL IS CALCULATED
      *  - TOTAL BILL IS CALCULATED
      *  - ACCUMULATE RECORD COUNT
      *******************************************

       110-PROCESS.
           IF WS-LINE-CNT >= 46
                PERFORM 200-HEADINGS
           END-IF

           MOVE BD-ACCOUNT TO
                GB-ACCOUNT
                EB-ACCOUNT
                DL-ACCOUNT.

           MOVE BD-CUST-NAME TO
                GB-CUST-NAME
                EB-CUST-NAME
                DL-CUST-NAME.

           MOVE BD-CUST-ADDRESS TO
                GB-CUST-ADDRESS
                EB-CUST-ADDRESS
                DL-ADDRESS.

           MOVE BD-KILOWATT TO
                EB-KILO-HOURS
                DL-KW-HOURS.

           MOVE BD-GAS TO
                GB-GAS-USED
                DL-GAS-USED.

           MOVE BD-ELEC TO
                EB-ELEC-BILL
                DL-ELEC-BILL.

           MOVE BD-GAS-BILL TO
                GB-GAS-BILL
                DL-GAS-BILL.

           COMPUTE TOT-BILL =
                BD-ELEC + BD-GAS-BILL
                MOVE TOT-BILL TO DL-TOTAL-BILL
                MOVE DETAIL-LINE TO PRINT-REC
                WRITE PRINT-REC
                   AFTER WS-LINES

           ADD WS-LINES TO WS-LINE-CNT
           ADD 1 TO TOT-CUST
           WRITE ELEC-BILL-DATA
           WRITE GAS-BILL-DATA
           MOVE 2 TO WS-LINES.
      *******************************************
      *  END OF JOB MODULE.
      *  - FILES CLOSED
      *******************************************

       120-END-OF-JOB.

           MOVE TOT-CUST TO TL-CUSTOMERS
           MOVE TOTAL-LINE TO PRINT-REC
           WRITE PRINT-REC
                AFTER 4
           CLOSE BILLING-INFO
                 GAS-BILL-INFO
                 ELEC-BILL-INFO.
       200-HEADINGS.
           MOVE TSO-ID TO PRINT-REC
           WRITE PRINT-REC
                 AFTER PAGE

           ADD 1 TO WS-PAGE-CNT
           MOVE WS-PAGE-CNT TO HL1-PAGE
           MOVE HEADING-LINE-1 TO PRINT-REC
           WRITE PRINT-REC

           MOVE HEADING-LINE-2 TO PRINT-REC
           WRITE PRINT-REC

           MOVE HEADING-LINE-3 TO PRINT-REC
           WRITE PRINT-REC
           AFTER 2

           MOVE HEADING-UNDER-LINE-1 TO PRINT-REC
           WRITE PRINT-REC
           AFTER 0

           MOVE HEADING-LINE-4 TO PRINT-REC
           WRITE PRINT-REC
           AFTER 4

           MOVE HEADING-LINE-5 TO PRINT-REC
           WRITE PRINT-REC

           MOVE HEADING-UNDER-LINE-2 TO PRINT-REC
           WRITE PRINT-REC
           AFTER 0

           MOVE 0 TO WS-LINE-CNT
           MOVE 3 TO WS-LINES.

